dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/tuxracer.h)
AM_INIT_AUTOMAKE(tuxracer, 0.10)
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_HEADER_TIME

dnl Checks for library functions.
AC_CHECK_FUNCS(getcwd gettimeofday strdup)

TCL_LIB_NAME=tcl

AC_ARG_WITH(tcl-libs,    [  --with-tcl-libs=DIR     Specify Tcl 8.0 library location],TCL_LIB_PATH="-L$withval")
AC_ARG_WITH(tcl-inc,     [  --with-tcl-inc=DIR      Specify Tcl 8.0 header file location],TUXRACER_EXTRA_CFLAGS="$TUXRACER_EXTRA_CFLAGS -I$withval")
AC_ARG_WITH(tcl-lib-name,[  --with-tcl-lib-name=NAME Specify Tcl 8.0 library base name],TCL_LIB_NAME="$withval")

dnl Check for Tcl >= 8.0
extra_libs="-lm $TCL_LIB_PATH"
AC_CHECK_LIB(dl, dlopen, extra_libs="$extra_libs -ldl")
AC_CHECK_LIB($TCL_LIB_NAME, Tcl_Free,,AC_MSG_ERROR(Tcl library version >= 8.0 not found),$extra_libs)

dnl Check for X11, taken partly from xracer sources

AC_PATH_XTRA

extra_libs="$X_LIBS $X_PRE_LIBS -lX11 -lXi -lXext -lXmu -lXt $X_EXTRA_LIBS $GL_LIB_PATH $extra_libs"

dnl Check for OpenGL.
dnl Taken partly from the xracer sources.

AC_CHECK_LIB(GL, glNewList,,,$extra_libs)

if test "x$ac_cv_lib_GL_glNewList" = "xno" ; then
	dnl Check for MesaGL.
	AC_CHECK_LIB(MesaGL, glNewList,,AC_MSG_ERROR(GL library was not found),$extra_libs)
fi

AC_CHECK_LIB(GLU, gluLookAt,,,$extra_libs)
if test "x$ac_cv_lib_GLU_gluLookAt" = "xno" ; then
        dnl Check for MesaGLU.
        AC_CHECK_LIB(MesaGLU, gluLookAt,,AC_MSG_ERROR(GLU library was not found),$extra_libs)
fi

opengl_LIBS="$LIBS $extra_libs $GLUT_LIB_PATH"

dnl Check for glut.
AC_CHECK_LIB(glut, glutKeyboardUpFunc,,AC_MSG_ERROR(GLUT library not found or too old version. 3.7 (beta) or later required.),$opengl_LIBS)

dnl Turn use of stencil buffer on/off
AC_ARG_ENABLE(stencil-buffer,[  --enable-stencil-buffer Use if your hardware has a stencil buffer],TUXRACER_EXTRA_CFLAGS="$TUXRACER_EXTRA_CFLAGS -DUSE_STENCIL_BUFFER")

dnl Specify data directory
AC_ARG_WITH(data-dir,[  --with-data-dir=PATH Default tuxracer data directory],TUXRACER_EXTRA_CFLAGS="$TUXRACER_EXTRA_CFLAGS -DDATA_DIR=\\\"$withval\\\"")

LIBS="$LIBS $extra_libs $GLUT_LIB_PATH"

AC_SUBST(TUXRACER_EXTRA_CFLAGS)

AC_OUTPUT(Makefile src/Makefile)
